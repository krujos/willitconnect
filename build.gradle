plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'willitconnect'
version = '1.0.13'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.json:json:20240303'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core'
}

jar {
    archiveBaseName = 'willitconnect'
    archiveVersion = '1.0.13'
}

tasks.named('wrapper') {
    gradleVersion = '9.1.0'
    distributionType = Wrapper.DistributionType.ALL
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-21'
    }
}

// Node.js tasks
node {
    version = '18.0.0'
    download = true
}

task webpack(type: NodeTask) {
    script = file('node_modules/webpack/bin/webpack')
    dependsOn npmInstall
}

task karma(type: NpmTask) {
    dependsOn npmInstall
    args = ['test']
}

project.tasks.processResources.dependsOn('npmInstall')
project.tasks.processResources.dependsOn('webpack')
project.tasks.test.dependsOn('karma')
