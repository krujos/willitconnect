plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'willitconnect'
version = '1.0.13'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.json:json:20240303'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.testng:testng:7.10.0'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
}

jar {
    archiveBaseName = 'willitconnect'
    archiveVersion = '1.0.13'
}

tasks.named('wrapper') {
    gradleVersion = '9.1.0'
    distributionType = Wrapper.DistributionType.ALL
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-25'
    }
}

// Node.js tasks
node {
    version = '20.11.0'
    download = true
    npmInstallCommand = 'install'
}

task npmInstallLegacy(type: NpmTask) {
    description = 'Install npm dependencies with legacy peer deps to resolve conflicts'
    args = ['install', '--legacy-peer-deps', '--strict-ssl=false']
    environment = [
        'NODE_TLS_REJECT_UNAUTHORIZED': '0',
        'GIT_TERMINAL_PROMPT': '0'
    ]
}

task webpack(type: NpmTask) {
    args = ['run', 'packit']
    dependsOn npmInstallLegacy
}

task karma(type: NpmTask) {
    dependsOn npmInstallLegacy
    args = ['test']
}

project.tasks.processResources.dependsOn('npmInstallLegacy')
project.tasks.processResources.dependsOn('webpack')
project.tasks.test.dependsOn('karma')

test {
    jvmArgs = [
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED'
    ]
}
